plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.7.10'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.7.10'

    id 'net.mamoe.mirai-console' version '2.14.0'
    id 'org.ajoberstar.grgit' version '5.0.0-rc.3' // Git 插件
}

group = 'xin.vanilla'
version = '2.0.0-dev'

repositories {
    maven { url 'https://maven.aliyun.com/repository/public' }
    maven { url "https://jitpack.io" }
    mavenCentral()
}

dependencies {
    implementation 'org.xerial:sqlite-jdbc:3.40.0.0' // Sqlite依赖
    implementation 'com.almworks.sqlite4java:sqlite4java:1.0.392'// Sqlite REGEXP函数依赖

    implementation 'io.github.karlatemp:caller:1.1.1'
    implementation 'com.aventrix.jnanoid:jnanoid:2.0.0' // Nano ID, 替代UUID
    implementation 'xin.vanilla.rcon:rcon:1.2.1' // MC RCON依赖
    implementation 'com.github.yangtu222:BeanUtils:1.0.11' // Bean工具类, 复制Bean
    implementation 'com.alibaba.fastjson2:fastjson2-kotlin:2.0.24'
    implementation 'cn.hutool:hutool-all:5.8.12' // 一堆工具类
    implementation 'com.github.houbb:pinyin:0.3.1' // 拼音工具类
    implementation 'com.github.oshi:oshi-core:6.2.2' // 系统信息工具类
    implementation 'com.unfbx:chatgpt-java:1.0.6' // ChatGPT三方库

    compileOnly 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'

    testImplementation 'junit:junit:4.13.2'

    testCompileOnly 'org.projectlombok:lombok:1.18.24'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.24'
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

// 获取 Git 当前分支的哈希值并加入到配置
tasks.register("appendGitHashToFile") {
    doLast {
        def gitCommitHash = grgit.head().abbreviatedId
        file('src/main/resources/hash').write("${gitCommitHash}")
    }
}

tasks.named('processResources') {
    // 先执行 appendGitHashToPluginYml 任务
    dependsOn "appendGitHashToFile"
}